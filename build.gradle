buildscript {
    ext {
        kotlinVersion = '1.2.21'
        springBootVersion = '1.5.9.RELEASE'
        springDependencyManagementVersion = '1.0.4.RELEASE'
        platformGroup = 'org.biacode.hermes.spring.netty'
        platformVersion = '0.6.0-SNAPSHOT'

        commonsLangVersion = '3.7'
        springBomVersion = 'Brussels-SR6'
        nettyVersion = '4.1.20.Final'
        propDepsPluginVersion = '0.0.9.RELEASE'

        easymockVersion = '3.5.1'
        assetjVersion = '3.9.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("io.spring.gradle:propdeps-plugin:${propDepsPluginVersion}")
    }
}

allprojects {

    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'kotlin-spring'
    apply plugin: "io.spring.dependency-management"

    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'

    if (System.env['DEPLOY_TO_NEXUS'] != null) {

        apply plugin: 'signing'

        signing {
            sign configurations.archives
        }

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                        authentication(userName: System.getenv('NEXUS_USERNAME'), password: System.getenv('NEXUS_PASSWORD'))
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                        authentication(userName: System.getenv('NEXUS_USERNAME'), password: System.getenv('NEXUS_PASSWORD'))
                    }

                    pom.project {
                        name 'NettySpring'
                        packaging 'jar'
                        // optionally artifactId can be defined here
                        description 'Netty spring integration.'
                        url 'https://github.com/hermes-chat/spring-netty'

                        scm {
                            connection 'scm:git@github.com:hermes-chat/spring-netty.git'
                            developerConnection 'scm:git:ssh://github.com:hermes-chat/spring-netty.git'
                            url 'https://github.com/hermes-chat/spring-netty/tree/master'
                        }

                        licenses {
                            license {
                                name 'Apache-2.0'
                                url 'https://opensource.org/licenses/Apache-2.0'
                            }
                        }

                        developers {
                            developer {
                                id 'biacode'
                                name 'Artur Asatryan'
                                email 'biacoder@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }

    group = platformGroup
    version = platformVersion
    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${springBomVersion}"
        }
    }

    dependencies {
        // Kotlin
        compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
        compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

        // Apache commons
        compile("org.apache.commons:commons-lang3:${commonsLangVersion}")

        // Logging
        compile("org.slf4j:slf4j-api")
        runtime("ch.qos.logback:logback-classic")

        // Test
        testCompile("junit:junit")
        testCompile("org.easymock:easymock:${easymockVersion}")
        testCompile("org.assertj:assertj-core:${assetjVersion}")
        testCompile("org.springframework:spring-test")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    compileJava.dependsOn(processResources)
}

project('core') {
    dependencies {
        compile("org.springframework:spring-context")
    }
}

project('router') {
    dependencies {
        compile project(':core')
    }
}

project('server') {
    dependencies {
        compile project(':router')

        compile("io.netty:netty-all:${nettyVersion}")
    }
}

project('websocket') {
    dependencies {
        compile project(':server')

        compile("io.netty:netty-codec-http:${nettyVersion}")
        compile("com.fasterxml.jackson.core:jackson-databind")
    }
}

project('starter') {
    dependencies {
        compile project(':websocket')

        compile("org.springframework.boot:spring-boot-starter")
        optional("org.springframework.boot:spring-boot-configuration-processor")
    }
}
